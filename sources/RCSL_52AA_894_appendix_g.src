; PROGRAM LOAD FROM DISK
; AND OTHER HIGH SPEED DEVICES

.LOC	0
DEV=	0

BEG:	READS	1		; READ SWITCHES
	LDA	0   C77		; ISOLATE DEVICE CODE
	AND	1,0
	NEG	0,0 SZR
LOOP:	ISZ	    OP1		; COUNT DEVICE CODE
	ISZ	    OP2		; INTO ALL IN/OUT
	ISZ	    OP3		; INSTRUCTIONS
	ISZ	    OP4
	INC	0,0 SZR		; DONE
	JMP	    LOOP	; NO, INCREMENT AGAIN
	MOVL	1,1 SZC		; DISK
	JMP	    OP3		; NO
	JSR	    SPEC	; SEEK WITH CLEAR
		    175000	; SEEK INSTRUCTION
	JSR	    SPEC	; RECALIBRATE WITH CLEAR
		    175400	; RECALIBRATE INSTRUCTION
	JSR	    SPEC	; DISK READY
		    175000
OP3:	DOAS	0   DEV		; START DEVICE WITH
				; RESET INSTRUCTION REGISTER
	LDA	2   C377	; SETUP JMP 377 IN
	STA	2   377		; LOCATION 377
OP4:	SKPBN	    DEV		; BUSY
	JMP	    OP3		; NO, START AGAIN
C377:	JMP	    377		; YES, WAIT PAGE ZERO
				; OVERWRITTEN

SPEC:	LDA	1   0,3		; DISK ROUTINE
OP1:	DOAP	1   DEV		; RECALIBRATE DISK
OP2:    DIA	1   DEV		; READ STATUS
	MOVS	1,2
	AND	1,2 SNR		; DONE
	JMP	OP2		; NO, WAIT
	JMP	1,3		; RETURN

C77:	77

.END
