; CARD READER PROGRAM LOAD II
;
; FOR LOADING OF PROGRAMS FROM A CARD READER CONNECTED
; TP CRC 705 OR EQUIVALENT
;
.LOC	0
CDR=16
;
	LDA	0,SA	; GET ADDRESS FOR CARD BUFFER
	JSR	GETCD	; GET ONE CARD
	JSR	CONV	; CONVERT ONE CARD TO WORDS
	LDA	0,110	; GET CARD NUMBER
	ADC	2,0,SZR	; CHECK FOR CARD NUMBER 1
	HALT		; CARD NUMBER ERROR
SA:	JMP	41	; GO TO PRE_LOADER
;
GETCD:	DOB	0,CDR	; OUTPUT BUFFER ADDRESS
	DOAS	0,CDR	; OUTPUT READ COMMAND
	SKPBN	CDR	; CHECK FOR THE READER IS STARTED
	JMP	.-2	; NO, TRY AGAIN
	SKPBZ	CDR	; WAIT FOR COMPLETION OF
	JMP	.-1	; READING A CARD
	JMP	0,3	; RETURN
;
; VARIABLES
;
C0UNT:	1
M50:	-50
ADDR1:	40
ADDR2:	40
;
; CONVERTING SUBROUTINE
;
CONV:	SUB	2,2	; CLEAR AC2
	LDA	0,M50	; GET COUNT FOR NUMBER OF WORDS
	STA	0,C0UNT	; STORE COUNT
LOOP:	LDA	0,@ADDR1; GET LEFT BYTE
	MOVS	0,0	; SWAPBYTE
	LDA	1,@ADDR1; GET RIGHT BYTE
	ADD	0,1	; COMPUTE WORD
	STA	1,@ADDR2; STORE WORD
	ADD	1,2	; COMPUTE CHECKSUM
	ISZ	C0UNT	; CHECK FOR MORE WORDS
	JMP	LOOP	; YES, GET THEM
	MOV	2,2,SZR	; CHECK FOR CHECKSUM ERROR
	HALT		; CHECKSUM ERROR
	JMP	0,3	; RETURN
.END
	
