; The Program Load Program in RC3803 autoboot ROM

.LOC	0
BEG:	READS	0	; READ SWITCHES INTO AC0
	MOVZL	0,1	; ISOLATE DEVICE CODE
	COMOR	1,1	; -DEVICE CODE - 1

LOOP:	ISZ	OP1	; COUNT DEVICE CODE INTO ALL
	ISZ	OP2	; IO INSTRUCTIONS
	ISZ	OP3
	ISZ	OP4
	INC	1,1,SZR	; DONE?
	JMP	LOOP	; NO INCREMENT AGAIN

OP1:	060077		; START DEVICE: (NIOS 0) - 1
	LDA	2,C377	; YES, PUT JMP 377 AND WAIT FOR CHANNEL
	STA	2,377
OP4:	063377		; BUSY?: (SKPBN 0) - 1
	JMP	OP1	; NO, GO TO OP1
	MOVL	0,0,SZC	; LOW SPEED DEVICE? (TEST SWITCH 0)
C377:	JMP	377	; NO, GO TO 377 AND WAIT FOR CHANNEL

LOOP2:	JSR	GET+1	; GET A FRAME
	MOVC	0,0,SNR	; IS IT NONZERO?
	JMP	LOOP2	; NO, IGNORE AND GET ANOTHER

LOOP4:	JSR	GET	; YES, GET FULL WORD
	STA	1,@C77	; STORE STARTING AT 100
	ISZ	100	; COUNT WORD - DONE ?
	JMP	LOOP4	; NO, GET ANOTHER
C77:	JMP	77	; YES - LOCATION COUNTER AND
			; JUMP TO LAST WORD
GET:	SUBZ	1,1	; CLEAR AC1, SET CARRY
OP2:
LOOP3:	063577		; DONE?: (SKPDN 0) - 1
	JMP	LOOP3	; NO, WAIT
OP3:	060477		; YES, READ INTO AC0: (DIAS 0,0) - 1
	ADDCS	0,1,SNC	; ADD 2 FRAMES SWAPPED - GOT SECOND ?
	JMP	LOOP3	; NO, GO BACK AFTER IT
	MOVS	1,1	; YES, SWAP AC1
	JMP	0,3	; RETURN WITH FULL WORD
.END
